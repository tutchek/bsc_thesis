\section{SICStus Prolog}
\label{implementation:sicstus}
\verb= $Id$ =


\subsection{N-Queens}
\begin{lstlisting}[language=Prolog]
:- use_module(library(clpfd)).

queens(N, Sol) :- 
  length(Queens, N),
  domain(Queens, 1, N),
  all_different(Queens),
  diagonals(Queens),
  labeling([], Queens),
  Sol = Queens.
  
create_list(0, []).
create_list(N, [_|T]) :- N > 0, NN is N - 1, create_list(NN, T).

diagonals([H|T]) :- diagonals([H|T], T).

diagonals([], _).
diagonals(_, []).
diagonals( [H1|T1], [H2|T2]) :- diagonals_mark(H1, [H2|T2], 1), diagonals(T1, T2).

diagonals_mark(_, [], _).
diagonals_mark(H, [H2|T2], A) :-
  H - H2 #\= A,
  H - H2 #\= -1 * A,
  AA is A + 1,
  diagonals_mark(H, T2, AA).
\end{lstlisting}

\subsection{Magic Sequence}
\begin{lstlisting}[language=Prolog]
:- use_module(library(clpfd)).

ms(N, Sol) :-
  length(Seq, N),
  domain(Seq, 0, N),
  seq_constraints(Seq),
  labeling([], Seq),
  Sol = Seq,
  write_sol(Sol).
  
seq_constraints(Seq) :-
  seq_constraints(Seq, Seq, 0).

seq_constraints([], _, _).
seq_constraints([H|T], Seq, A) :-
  create_sum(Seq, A, Sum),
  sum(Sum, #=, H),
  AA is A+1,
  seq_constraints(T, Seq, AA).

create_sum([], _, []).
create_sum([H|T], Val, [X|T2]) :-
  X in 0..1,
  H #= Val #<=> X #= 1,
  create_sum(T, Val, T2).

write_sol([]).  
write_sol([H|T]) :-
  write(H),
  write(' '),
  write_sol(T).
\end{lstlisting}

\subsection{Self-refferential Quiz}
\label{implementation:sicstus:srq}
\begin{lstlisting}[language=Prolog]
:- use_module(library(clpfd)).

srq(Sol) :-
  length(Srq, 50),
  domain(Srq, 0, 1),
  fdbg_assign_name(Srq, srq),
  each_question_answer(Srq),
  
  questions12345(Srq),
  questions678910(Srq),
  
  labeling([], Srq),
  Sol = Srq.

each_question_answer(Srq) :-
  each_question_answer(Srq, 0).
  
each_question_answer(_, 10).
each_question_answer(Srq, A) :-
  A < 10,
  AA is A + 1,
  select_row(5, A, Srq, Row),
  sum(Row, #=, 1),
  !,
  each_question_answer(Srq, AA).

questions12345([A1,B1,C1,D1,E1,
           A2,B2,C2,D2,E2,
           A3,B3,C3,D3,E3,
           A4,B4,C4,D4,E4,
           A5,B5,C5,D5,E5,
           A6,B6,C6,D6,E6,
           A7,B7,C7,D7,E7,
           A8,B8,C8,D8,E8,
           A9,B9,C9,D9,E9,
           A10,B10,C10,D10,E10]) :-
           
           % Q1
           (A1 #= 1) #<=> ((A4 #= 1) #/\ (A1 #= 0) #/\ (A2 #= 0) #/\ (A3 #= 0)),
           (B1 #= 1) #<=> ((A3 #= 1) #/\ (A1 #= 0) #/\ (A2 #= 0)),
           (C1 #= 1) #<=> ((A2 #= 1) #/\ (A1 #= 0)),
           (D1 #= 1) #<=> ((A1 #= 1)),
           (E1 #= 1) #<=> ((A4 #= 0) #/\ (A1 #= 0) #/\ (A2 #= 0) #/\ (A3 #= 0)),
           
           % Q2
           (A2 #= 1) #<=> ((A3 #= A4) #/\ (B3 #= B4) #/\ (C3 #= C4) #/\ (D3 #= D4) #/\ (E3 #= E4)),
           (B2 #= 1) #<=> ((A4 #= A5) #/\ (B4 #= B5) #/\ (C4 #= C5) #/\ (D4 #= D5) #/\ (E4 #= E5)),
           (C2 #= 1) #<=> ((A5 #= A6) #/\ (B5 #= B6) #/\ (C5 #= C6) #/\ (D5 #= D6) #/\ (E5 #= E6)),
           (D2 #= 1) #<=> ((A6 #= A7) #/\ (B6 #= B7) #/\ (C6 #= C7) #/\ (D6 #= D7) #/\ (E6 #= E7)),
           (E2 #= 1) #<=> ((A7 #= A8) #/\ (B7 #= B8) #/\ (C7 #= C8) #/\ (D7 #= D8) #/\ (E7 #= E8)),
           
           % Q3
           (A3 #= 1) #<=> (A4 #= 1),
           (B3 #= 1) #<=> ((A4 #= 0) #/\ (A5 #= 1)),
           (C3 #= 1) #<=> ((A4 #= 0) #/\ (A5 #= 0) #/\ (A6 #= 1)),
           (D3 #= 1) #<=> ((A4 #= 0) #/\ (A5 #= 0) #/\ (A6 #= 0) #/\ (A7 #= 1)),
           (E3 #= 1) #<=> ((A4 #= 0) #/\ (A5 #= 0) #/\ (A6 #= 0) #/\ (A7 #= 0) #/\ (A8 #= 1)),
           
           % Q4
           (A4 #= 1) #<=> (B2 #= 1),
           (B4 #= 1) #<=> ((B2 #= 0) #/\ (B4 #= 1)),
           (C4 #= 1) #<=> ((B2 #= 0) #/\ (B4 #= 0) #/\ (B6 #= 1)),
           (D4 #= 1) #<=> ((B2 #= 0) #/\ (B4 #= 0) #/\ (B6 #= 0) #/\ (B8 #= 1)),
           (E4 #= 1) #<=> ((B2 #= 0) #/\ (B4 #= 0) #/\ (B6 #= 0) #/\ (B8 #= 0) #/\ (B10 #= 1)),
           
           % Q5
           (A5 #= 1) #<=> (C1 #= 1),
           (B5 #= 1) #<=> (C3 #= 1),
           (C5 #= 1) #<=> (C5 #= 1),
           (D5 #= 1) #<=> (C7 #= 1),
           (E5 #= 1) #<=> (C9 #= 1).
           
           % Q6
questions678910([A1,B1,C1,D1,E1,
           A2,B2,C2,D2,E2,
           A3,B3,C3,D3,E3,
           A4,B4,C4,D4,E4,
           A5,B5,C5,D5,E5,
           A6,B6,C6,D6,E6,
           A7,B7,C7,D7,E7,
           A8,B8,C8,D8,E8,
           A9,B9,C9,D9,E9,
           A10,B10,C10,D10,E10]) :-
           
           sum([D1,D2,D3,D4,D5], #=, Before),
           sum([D7,D8,D9,D10], #=, After),
           (A6 #= 1) #<=> ((Before #> 0) #/\ (After #= 0)),
           (B6 #= 1) #<=> ((Before #= 0) #/\ (After #> 0)),
           (C6 #= 1) #<=> ((Before #> 0) #/\ (After #> 0)),
           (D6 #= 1) #<=> ((Before #= 0) #/\ (After #= 0) #/\ (D6 #= 0)),
           (E6 #= 1) #<=> (D6 #= 1),
           
           % Q7
           (A7 #= 1) #<=> ((E5 #= 1) #/\ (E6 #= 0) #/\ (E7 #= 0) #/\ (E8 #= 0) #/\ (E9 #= 0) #/\ (E10 #= 0)),
           (B7 #= 1) #<=> ((E6 #= 1) #/\ (E7 #= 0) #/\ (E8 #= 0) #/\ (E9 #= 0) #/\ (E10 #= 0)),
           (C7 #= 1) #<=> ((E7 #= 1) #/\ (E8 #= 0) #/\ (E9 #= 0) #/\ (E10 #= 0)),
           (D7 #= 1) #<=> ((E8 #= 1) #/\ (E9 #= 0) #/\ (E10 #= 0)),
           (E7 #= 1) #<=> ((E9 #= 1) #/\ (E10 #= 0)),
           
           % Q8
           sum([B1, C1, D1, B2, C2, D2, B3, C3, D3, B4, C4, D4, B5, C5, D5, B6, C6, D6, B7, C7, D7, B8, C8, D8, B9, C9, D9, B10, C10, D10], #=, Consonants),
           (A8 #= 1) #<=> (Consonants #= 7),
           (B8 #= 1) #<=> (Consonants #= 6),
           (C8 #= 1) #<=> (Consonants #= 5),
           (D8 #= 1) #<=> (Consonants #= 4),
           (E8 #= 1) #<=> (Consonants #= 3),
           
           % Q9
           sum([A1, E1, A2, E2, A3, E3, A4, E4, A5, E5, A6, E6, A7, E7, A8, E8, A9, E9, A10, E10], #=, Vowels),
           (A9 #= 1) #<=> (Vowels #= 0),
           (B9 #= 1) #<=> (Vowels #= 1),
           (C9 #= 1) #<=> (Vowels #= 2),
           (D9 #= 1) #<=> (Vowels #= 3),
           (E9 #= 1) #<=> (Vowels #= 4).

select_row(N, RowId, Matrix, Row) :-  
  BoundL is RowId*N, 
  BoundH is (RowId+1)*N-1, 
  select_row(N, Matrix, Row, 0, BoundL, BoundH).
select_row(_, [], [], _, _, _).
select_row(_, _, [], A, _, BH) :- BH < A.

select_row(N, [H|T], [H|RT], A, BL, BH) :-
  A >= BL,
  A =< BH,
  AA is A + 1,
  select_row(N, T, RT, AA, BL, BH).
  
select_row(N, [_|T], RT, A, BL, BH) :-
  A < BL,
  AA is A + 1,
  select_row(N, T, RT, AA, BL, BH).
  
print_srq(Srq) :-
  print_srq(Srq, 0).

print_srq(_, 10).  
print_srq(Srq, A) :-
  A < 10,
  select_row(5, A, Srq, Row), 
  write(A),
  write(Row), 
  write('\n'),
  AA is A + 1,
  !,
  print_srq(Srq, AA).
\end{lstlisting}

\subsection{Quasigroup With Holes}
\begin{lstlisting}[language=Prolog]
:- use_module(library(clpfd)).

qwh(N, Sol) :-
  NN is N*N,
  length(Matrix, NN),
  domain(Matrix, 1, N),
  q_constraint(N, Matrix),
  labeling([], Matrix),
  Sol = Matrix.

write_solution(N, Sol) :-
  NN is N - 1, write_solution(N, NN, Sol, 0).

write_solution(_, _, [], _).
write_solution(N, NN, [H|T], A) :-
  M is A mod N,
  write(H),
  (M = NN, write('\n') ; M \= NN, write(' ')),
  AA is A + 1,
  write_solution(N, NN, T, AA).
  
select_column(N, ColId, Matrix, Col) :- 
  select_column(N, ColId, Matrix, Col, 0).

select_column(_, _, [], [], _).
select_column(N, ColId, [H|T], [H|CT], A) :-
  ColId is A mod N,
  AA is A + 1,
  select_column(N, ColId, T, CT, AA).
select_column(N, ColId, [_|T], CT, A) :-
  Mod is A mod N,
  Mod \= ColId,
  AA is A + 1,
  select_column(N, ColId, T, CT, AA).
  
  
select_row(N, RowId, Matrix, Row) :-  
  BoundL is RowId*N, 
  BoundH is (RowId+1)*N-1, 
  select_row(N, Matrix, Row, 0, BoundL, BoundH).

select_row(_, [], [], _, _, _).
select_row(_, _, [], A, _, BH) :- BH < A.

select_row(N, [H|T], [H|RT], A, BL, BH) :-
  A >= BL,
  A =< BH,
  AA is A + 1,
  select_row(N, T, RT, AA, BL, BH).
  
select_row(N, [_|T], RT, A, BL, BH) :-
  A < BL,
  AA is A + 1,
  select_row(N, T, RT, AA, BL, BH).
  

q_constraint(N, Matrix) :-
  q_constraint(N, Matrix, 0).
  
q_constraint(N, _, M) :- M >= N.
q_constraint(N, Matrix, A) :- 
  A < N,
  select_row(N, A, Matrix, Row),
  select_column(N, A, Matrix, Col),
  all_different(Row),
  all_different(Col),
  AA is A + 1,
  q_constraint(N, Matrix, AA).
\end{lstlisting}

\subsection{Locating warehouses}
\label{implementation:sicstus:warehouses}
\begin{lstlisting}[language=Prolog]
:- use_module(library(clpfd)).

warehouses(OC, SC, WC, Sol, TotalCost) :-
  length(WC, WLength),
  length(SC, SLength),
  length(Supplier, SLength),
  length(Cost, SLength),
  length(Open, WLength),
  TotalCost in 0..sup,
  WLength1 is WLength-1,
  domain(Supplier, 0, WLength1),
  NumberOpen in 0..WLength,
  domain(Cost, 0, sup),
  CostSum in 0..sup,
  NumberOpen in 0..WLength,
  domain(Open, 0, 1),
  
  TotalCost #= CostSum + NumberOpen * OC,
  sum(Cost, #=, CostSum),
  sum(Open, #=, NumberOpen),
  
  set_supplier(Supplier, Cost, SC),
  set_open(Open, Supplier, WC),
  
  minimize(labeling([], Supplier), TotalCost),
  Sol = Supplier.

set_open(Open, Supplier, WC) :-
  set_open(Open, Supplier, WC, 0).
  
set_open([], _, _, _).
set_open([H|T], Supplier, [HWC|TWC], A) :-
  create_open(Supplier, Aux, A),
  sum(Aux, #=, Sum),
  (Sum #> 0) #<=> (H #= 1),
  Sum #=< HWC,
  AA is A+1,
  set_open(T, Supplier, TWC, AA).

create_open([], [], _).
create_open([H|T], [HA|TA], A) :-
  HA in 0..1,
  (H #= A) #<=> (HA #= 1),
  create_open(T, TA, A).

set_supplier([], _, _).
set_supplier([HS|TS], [HC|TC], [HSC|TSC]) :-
   set_supplier2(HS, HSC, HC, 0),
   set_supplier(TS, TC, TSC).
   
set_supplier2(_, [], _, _).
set_supplier2(S, [H|T], C, A) :-
  (S #= A) #<=> (C #= H),
  AA is A + 1,
  set_supplier2(S, T, C, AA).
\end{lstlisting}